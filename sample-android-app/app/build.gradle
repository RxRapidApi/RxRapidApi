buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'me.tatarka:gradle-retrolambda:3.3.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    jcenter()
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

def zillowApiKey = properties.getProperty('zillow.api.key')
def rapidApiProject = properties.getProperty('rapidapi.project.name')
def rapidApiKey = properties.getProperty('rapidapi.api.key')

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "com.gatebuzz.rapidapi.rx.example"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "PROJECT", "\"${rapidApiProject}\""
        buildConfigField "String", "API_KEY", "\"${rapidApiKey}\""
        buildConfigField "String", "ZILLOW_API_KEY", "\"${zillowApiKey}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // Depend on production version
    // compile 'com.gatebuzz:rxrapidapi:0.7.3'

    // Depend on local development version
    compile files('../../sdk/build/libs/RxRapidApiSdk.jar')
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.okio:okio:1.9.0'
    compile 'com.google.code.gson:gson:2.8.0'

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
}
